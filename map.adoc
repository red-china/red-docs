= Map!数据类型
:toc:
:toc-title: 目录
:numbered:

== 概要(Abstract)

`map`表示一个拥有键/值对的关联数组。它提供了一种快速访问(内部使用哈希表)、便捷和专用的语法。和`hash!`不同的是，`map`不是一个序列，所以它不存在偏移和位置的概念。理论上讲，`map!`是一种处于`hash!`和`object!`之间的数据类型。

== 字面量语法(Literal syntax)
----
#(<key> <value>...)

<key>   : hashed key, accepted types are: scalar!, all-word!, any-string!
<value> : any-type! value
----
== 构造语法(Creation syntax)
----
make map! <spec>

<spec> : block of key/value pairs or an integer value
----
如果_spec_是个整数，一个包含预分配大小的空`map!`将会被返回(通常是为了动态计算`map`)。

注意：

* `map` body或_spec_ block必须包含偶数个元素，否则会产生错误。
* 值不是经过*reduce*产生的，所以某些值必须使用构造语法，比如`logic!`。

例子：
----
#(a: 1 b: 2)
== #(
    a: 1
    b: 2
)

make map! [a 1 'b 2 "c" 3]
== #(
    a: 1
    b: 2
    "c" 3
)
----
如果键是**any-word!**类型，为了使`map`的内容看起来更像键/值对，在`map`中键的类型会被转换为*set-word!*。要访问这些键，不需要再提供`set-word`，简单的`word`就够了。同样地，`keys-of`反射器会返回`word`而不是`set-word`，这大大简化了之后的处理过程(特别是匹配操作，匹配`word`比匹配`set-word`容易多了)。

注意：

* 和`hash!`以及`block!`一样, `map!`的查找操作默认是**大小写不敏感的**。
* 如果指定了`none`作为值，那么相应的键不会被创建(参考”删除键“部分)。
* 在创建`map`的时候所有的键都是深度拷贝的。
* 在创建`map`的时候，如果值是`series!`类型，则值不会被拷贝，此选项留给用户(这样可以优化常见用例的资源)。

另一种创建`map`的方式是使用`copy`动作来复制一个已有的`map`。

== 获取值(Retrieving values)

使用路径：
----
<map>/<key>
get '<map>/<key>

<map> : word referring to a map! value
<key> : word key
----

使用`select`动作：
---- 
select <map> <key>

<map> : map value
<key> : any valid key type
----
所有获取值的操作都是大小写不敏感的，如果想要区分大小写，需要用`/case`细化：
----
get/case '<map>/<key>
select/case <map> <key>
----
如果访问一个`map`中不存在的键，结果将是`none`。

例子：

----
m: #(Ab: 2 aB: 5 ab: 10)
m/ab
== 2
select m 'aB
== 2
get/case 'm/aB
== 5
select/case m 'ab
== 10
----

== 改变键和值(Changing keys and values)

使用路径：
----
<map>/<key>: <value>
set '<map>/<key> <value>

<map>   : word referring to a map! value
<key>   : word key to select a value in the map
<value> : any value
----

使用修改动作：
---- 
put <map> <key> <value>

<map> : map value
<key> : any valid key value to a select a value in the map
----
修改大量数据：
----
extend <map> <spec>

<map>  : a map value
<spec> : block of name/value pairs (one or more pairs)
----
所有获取值的操作都是大小写不敏感的，如果想要区分大小写，需要用`/case`细化：
----
set/case '<map>/<key> <value>
put/case <map> <key> <value>
extend/case <map> <spec>
----

`extend`可以一次性接收多个键，所以它很适合用来修改大量数据。

注意：

* 如果设置一个map中不存在的键，将会**自动创建它**.
* 如果添加的键和`map`原有的键同名，则`map`中的键会被更新(默认情况下键是不区分大小写的)。

例子：
----
m: #(Ab: 2 aB: 5 ab: 10)
m/ab: 3
m
== #(
    Ab: 3
    aB: 5
    ab: 10
)

put m 'aB "hello"
m
== #(
    Ab: "hello"
    aB: 5
    ab: 10
)

set/case 'm/aB 0
m
== #(
    Ab: "hello"
    aB: 0
    ab: 10
)
set/case 'm/ab 192.168.0.1
== #(
    Ab: "hello"
    aB: 0
    ab: 192.168.0.1
)

m: #(%cities.red 10)
extend m [%cities.red 99 %countries.red 7 %states.red 27]
m
== #(
    %cities.red 99
    %countries.red 7
    %states.red 27
)
----

== 删除键(Deleting keys)

要删除`map`中的键/值，只要设置键为`none`即可，具体可以采用以下几种方式：

例子：
----
m: #(a: 1 b 2 "c" 3 d: 99)
m
== #(
    a: 1
    b: 2
    "c" 3
    d: 99
)
m/b: none
put m "c" none
extend m [d #[none]]
m
== #(
    a: 1
)
----

注意：上面例子中传递`none!`类型的值时必须使用构造语法。

你也可以用`clear`动作一次性清空`map`：
----
clear #(a 1 b 2 c 3)
== #()
----

== 反射(Reflection)

* `find` 检查`map`中是否存在指定的键。如果存在返回`true`，否则返回`none`。

 find #(a 123 b 456) 'b
 == true

* `length?` 返回`map`中键/值对的个数。

 length? #(a 123 b 456)
 == 2

* `keys-of` 返回包含`map`中所有键的`block`(set-words被转换为words)。

 keys-of #(a: 123 b: 456)
 == [a b]

* `values-of` 返回包含`map`中所有值的`block`。

 values-of #(a: 123 b: 456)
 == [123 456]

* `body-of` 返回包含`map`中所有键/值对的`block`。

 body-of #(a: 123 b: 456)
 == [a: 123 b: 456]
