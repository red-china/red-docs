= 响应式编程
:imagesdir: ../images
:toc:
:numbered:

== 概念 

自版本0.6.0开始，Red引入响应式编程，以精简程序。 响应模式依赖于数据流和对象事件，以构造有向图及在对象间传递变化。它使用推送（push）模式，更准确讲，是 https://en.wikipedia.org/wiki/Reactive_programming#Object-oriented[对象响应式编程] 模式，仅对象字段是变化之源。

虽然描述的抽象，但响应式 API 及其使用，简单而实用。下面是一些图表，帮助可视化响应关系。

image::react-simple.png[react-simple,align="center"]

_图 A & B 展示了一或多个响应器（Reactor:作为响应源的对象）之间的简单关系。_

image::react-graphs.png[react-graph,align="center"]


_图 C, D & E 展示响应关系链, 其中某些目标自身就是响应器（Reactor）, 使得关系链可为任何形状。_

当源字段的值变化时，响应是异步的。响应关系会维持到使用  `react/unlink` 或 `clear-reactions` 明确销毁响应之时。

响应表达式中，仅源对象需要是响应器，目标可以是简单对象。若目标对象也是响应器，则响应关系是链式的，并且会隐式构造关系图。

备注：

* Red 的响应式编程将来会支持"拉取"（pull）模式。
* 虽然将来会支持事件流，但这并不是 https://en.wikipedia.org/wiki/Functional_reactive_programming[函数响应式编程（FRP）] 框架。
* Red/View GUI 引擎依靠 _face!_ 对象操作图形界面. face 都是响应器, 它们彼此之间，或与非响应器对象之间，可建立响应关系。

=== 术语 

[cols="1,2", options="header"]
|===
|名称 |  定义
|响应式编程|  一种编程范式，数据流编程的子集，基于事件 `推送` 变化。
|响应|  包含一或多个响应表达式的代码区块。
|响应表达式|  至少涉及一个响应源的表达式。
|响应关系|  在两个或多个对象之间，用响应表达式实现的一种关系。
|响应源| 一个引用响应对象内字段的路径（path!）值。
|响应公式|  一种返回运算中最末表达式结果的响应。
|响应对象|  其字段被用作响应源的对象。
|响应器|	 `响应对象` 的别称。
|===

=== 静态关系 

响应的最简单形式，是创建于 _命名_ 对象之间的 `静态关系` 。因其静态地链接对象，故称静态关系。它仅适用于源响应器，而不能再用于其它对象。

*例 1*

----
view [
    s: slider return
    b: base react [b/color/1: to integer! 255 * s/data]
]
----
此例，在命名为 `s` 的 slider 与命名为 `b` 的 base 之间建立响应关系。使得 base 的背景色依据 slider 的移动而变化。此响应表达式不得再使用于其它 face 。这是 Red/View 中，图形对象间响应行为的最简单方式。

*例 2*

    vec: make reactor! [x: 0 y: 10]
    box: object [length: is [square-root (vec/x ** 2) + (vec/y ** 2)]]

静态关系的另一种形式可用 `is` 操作符定义。它把响应运算的值赋给任一语境中的字。

此例无关于GUI系统。它用响应表达式计算由 `vec/x` 与 `vec/y` 定义的向量的长度。再次地，表达式中，源对象是由名称（`vec`）静态地指定的。

*例 3*

	a: make reactor! [x: 1 y: 2 total: is [x + y]]
	
上面，字 `total` 的值由表达式 `x+y` 赋与。每当 `x` 或 `y` 的值有变化， `total` 的值会立即更新。注意，此时无需路径指定响应源，因为  `is` 直接用在响应器内，而自知语境。

*例 4*

	a: make reactor! [x: 1 y: 2]
	total: is [a/x + a/y]

此例3的变体展示了，一个根语境的字，也可为响应关系的目标（虽不能为源）。这种形式最接近于电子表格（如Excel)的公式模型。

备注：尽管将来会克服，但根语境由于尺寸原因，其响应化（如上面的 `total` ）将有显著的性能开销。

=== 动态关系 

静态关系虽容易设定，但若需要提供同一个响应给多个响应器，或响应器是匿名的（记住：所有对象默认都是匿名的），则适用性欠佳。此类情况，应当用 *函数* 与 `react/link` 来设定响应。

*示例*

----
;-- 用鼠标上下拖动红球，看别的球如何响应。

win: layout [
    size 400x500
    across
    style ball: base 30x30 transparent draw [fill-pen blue circle 15x15 14]
    ball ball ball ball ball ball ball b: ball loose
    do [b/draw/2: red]
]

follow: func [left right][left/offset/y: to integer! right/offset/y * 108%]

faces: win/pane
while [not tail? next faces][
    react/link :follow [faces/1 faces/2]
    faces: next faces
]
view win
----
例中的响应是个应用于成对小球的函数（ `follow` ）。由此构造出关系链连接所有小球。而响应条件是参数式的，故可应用于不同对象（有别于静态关系）。


== API 

=== react 

*语法*

----
react <code>
react/unlink <code> <source>

react/link <func> <objects>
react/unlink <func> <source>

react/later <code>

<code>    : 包含至少一个响应源的代码块(block!)
<func>    : 包含至少一个响应源的函数(function!)
<objects> : 用作响应函数参数的对象列表 (block! of object! values).
<source>  : 'all word, or an object or a list of objects (word! object! block!).

Returns   : <code> 或 <func>  对响应的进一步引用？（for further references to the reaction.
----
*说明*
 
`react` 从代码块（设置“静态关系”）或函数（设置“动态关系”，且需要 `/link` 饰字）设置至少含一个响应源的新的响应关系，
两种情况下，代码都是静态分析以确定引用响应器字段的响应源（以路径值(path!)的形式）。
默认，新形成的响应会在 `react` 函数返回之前的*创建时调用一次*。这在某些情况下不可取，得使用 `/later`选项。
 
响应包含任意Red代码，一或多个响应源，及一或多个响应表达式。由用户根据需要设置合适的关系。
 
`/link` 选项携带一个函数作为响应，且一个对象参数列表用来运算该响应。
The `/link` option takes a function as the reaction and a list of arguments objects to be used in evaluation of the reaction. This alternative form allows dynamic reactions, where the reaction code can be reused with different sets of objects (the basic `react` can only work with statically _named_ objects).

A reaction is removed using the `/unlink` refinement and with one of the following as a `<source>` argument:

* The `'all` word, will remove all reactive relations created by the reaction.
* An object value, will remove only relations where that object is the reactive source.
* A list of objects, will remove only relations where those objects are the reactive source.

`/unlink` takes a reaction block or function as argument, so only relations created from *that* reaction are removed.

=== is 

*Syntax*
----
<word>: is <code>

<word> : word to be set to the result of the reaction (set-word!).
<code> : block of code that contain at least one reactive source (block!).
----
*Description*

`is` creates a reactive formula whose result will be assigned to a word. The `<code>` block can contain references to both the wrapping object's fields, if used in a reactor's body block, and to external reactor's fields.

NOTE: This operator creates reactive formulas which closely mimic Excel's formula model.

*Example*
----
a: make reactor! [x: 1 y: 2 total: is [x + y]]

a/total
== 3
a/x: 100
a/total
== 102
----
=== react? 

*Syntax*
----
react? <obj> <field>
react?/target <obj> <field>

<obj>   : object to check (object!).
<field> : object's field to check (word!).

Returns : a reaction (block! function!) or a none! value.
----
*Description*

`react?` checks if an object's field is a reactive source . If it is, the first reaction found where that object's field is present as a source, will be returned, otherwise `none` is returned. `/target` refinement checks if the field is a target instead of a source, and will return the first reaction found targeting that field or `none` if none matches.

=== clear-reactions  

*Syntax*
----
clear-reactions
----
*Description*

Removes all defined reactions, unconditionally.

=== dump-reactions 

*Syntax*
----
dump-reactions
----
*Description*

Outputs a list of registered reactions for debug purposes.

== Reactive Objects  

Ordinary objects in Red do not exhibit reactive behaviors. In order for an object to be a reactive source, it needs to be constructed from one of the following reactor prototypes.

=== reactor!  

*Syntax*
----
make reactor! <body>

<body> : body block of the object  (block!).

Returns : a reactive object.
----
*Description*

Constructs a new reactive object from the body block. In the returned object, setting a field to a new value will trigger reactions defined for that field.

NOTE: The body may contain `is` expressions.

=== deep-reactor! 

*Syntax*
----
make deep-reactor! <body>

<body> : body block of the object  (block!).

Returns : a reactive object.
----
*Description*

Constructs a new reactive object from the body block. In the returned object, setting a field to a new value or changing a series the field refers to, including nested series, will trigger reactions defined for that field.

NOTE: The body may contain `is` expressions.

*Example*

This shows how change to a serie, even a nested one, triggers a reaction.

NOTE: It is up to the user to prevent cycles at this time. For example, if a `deep-reactor!` changes series values in a reactor formula (e.g. `is`), it may create endless reaction cycles.
----
r: make deep-reactor! [
    x: [1 2 3]
    y: [[a b] [c d]]
    total: is [append copy x copy y]
]
append r/y/2 'e
print mold r/total
----
